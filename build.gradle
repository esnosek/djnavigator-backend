plugins {
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.2'
}

group = 'dev.nos'
version = '1.0.0'

jar {
    archiveBaseName = 'dj-navigator-backend'
    archiveVersion = '1.0.0'
}

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    integration_testImplementation.extendsFrom(testImplementation)
    integration_testRuntimeOnly.extendsFrom(testRuntimeOnly)
}

repositories {
    mavenCentral()
}

sourceSets {
    integration_test {
        groovy {
            srcDir "$projectDir/src/integration_test/groovy"
            resources.srcDir "$projectDir/src/integration_test/resources"
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

tasks.register('integrationTest', Test) {
    useJUnitPlatform()
    description = "Run integration tests"
    group = "verification"
    testClassesDirs = sourceSets.integration_test.output.classesDirs
    classpath = sourceSets.integration_test.runtimeClasspath
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy 'exclude'
}

dependencies {
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    compileOnly 'org.springframework.boot:spring-boot-starter-webflux'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.unbroken-dome.base62:base62:1.1.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    implementation 'org.postgresql:postgresql'

    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:3.2.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework:spring-webflux'
    testImplementation 'org.apache.groovy:groovy-json:4.0.14'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.3-groovy-4.0'
    testRuntimeOnly 'org.apache.groovy:groovy-all:4.0.14'
    testRuntimeOnly 'com.h2database:h2'
}

tasks.named('test') {
    useJUnitPlatform()
}
